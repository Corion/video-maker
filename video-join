#!/usr/bin/perl
use strict;
use warnings;

use Getopt::Long;
use YAML 'LoadFile';
use File::Temp 'tempfile';
use Cwd;
use Text::ParseWords;

GetOptions(
    'o=s'  => \my $outname,
    'safe' => \my $safe,
    'v'    => \my $verbose,
);

my $ffmpeg = $ENV{FFMPEG} || 'ffmpeg';
my @ffmpeg_options = shellwords( $ENV{FFMPEG_OPT});

my ($fh, $listname) = tempfile(UNLINK => 1);
my $cwd = getcwd();

my ($config) = grep /\.yml/i, @ARGV;
if( $config ) {
    $config = LoadFile( $config );
} else {
    $config = {}
};

print "Join: $_\n" for @ARGV;

print $fh join( "\n", map { qq(file '$cwd/$_') } grep { !/yml$/i}@ARGV);
close $fh;

my @metadata;
for my $meta (keys %{ $config->{metadata} || {} }) {
    push @metadata, '-metadata', $meta . '=' . $config->{metadata}->{$meta}
        if exists $config->{metadata}->{$meta};
};

my @cmd = ( $ffmpeg =>
            @ffmpeg_options,
            '-f', 'concat',
            '-analyzeduration', 25000,
            '-safe', ($safe ? '1' : '0' ),
            '-i', $listname,
           #'-c', 'copy',
           '-c:a', 'aac',
	       #'-c:v', 'libx264',
	       '-c:v', 'h264_nvenc',
            @metadata,
            '-movflags',
            '+faststart',
            $outname, '-y'
);
warn "@cmd" if $verbose;
system(@cmd) == 0
    or die "$? / $!";
